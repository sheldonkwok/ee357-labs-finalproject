#  NOTE: The debugger uses the Illegal Instruction Vector to stop.
#  A small subroutine is written at the location VBR+0x408-VBR+0x40B
#  to handle the exception.  The Illegal Instruction Vector in
#  the the vector table at VBR+0x10 is then pointed to it.  When the
#  debugger encounters an illegal instruction, it jumps to this 
#  subroutine, which ends with an RTE, then exits.
#  Do not overwrite this area of memory otherwise the debugger may not 
#  exit properly.

MEMORY {
	vector_ram (RWX) : ORIGIN = 0x20000000, LENGTH = 0x00000418
	user       (RWX) : ORIGIN = 0x20000420, LENGTH = 0x00007BD0
	ipsbar     (RWX) : ORIGIN = 0x40000000, LENGTH = 0x0
}

SECTIONS {
	.ipsbar		: {} > ipsbar

	.vectors :
	{
		. = ALIGN (0x4);
	} > vector_ram

	.text :
	{
		*(.text)
		.= ALIGN(0x10);
		*(.rodata)		
		.= ALIGN(0x10);
	} > user

    .data : 
	{
		___DATA_ROM = .;
		___DATA_RAM = .;
	
		*(.exception)	
		.                         = ALIGN(0x4);	
		__exception_table_start__ = .;
		EXCEPTION
		__exception_table_end__   = .;
		
		___sinit__   = .;
	    STATICINIT

		__START_DATA = .;
		*(.data)
		*(.relocate_code)
		.            = ALIGN (0x4);
		__END_DATA   = .;

		__START_SDATA = .;
		*(.sdata)
		.             = ALIGN (0x4);
		__END_SDATA   = .;

		__SDA_BASE = .;
		.          = ALIGN(0x4);
	} >> user

	.bss :
	{
		__START_SBSS = .;
		*(.sbss)
		*(SCOMMON)
		__END_SBSS   = .;

		__START_BSS  = .;
		*(.bss)
		*(COMMON)
		__END_BSS    = .;	
		. = ALIGN(0x4);
	} >> user

	___FLASH        = 0;
	___FLASH_SIZE	= 0x00080000;
	__S_romp        = 0x0;					    # no ROM in this example

	___SRAM			= ADDR(.vectors);
	___SRAM_SIZE	= 0x00010000;

	___IPSBAR		= ADDR(.ipsbar);

	___SP_SIZE      = 0x1000;
    ___HEAP_START	= .;
    ___HEAP_END		= ___SRAM + ___SRAM_SIZE - ___SP_SIZE;
    ___SP_END		= ___HEAP_END;
    ___SP_INIT		= ___SP_END + ___SP_SIZE;

	___heap_addr 	= ___HEAP_START;
	___heap_size 	= ___HEAP_END -  ___HEAP_START;
	__SP_INIT 		= ___SP_INIT;

    ___VECTOR_RAM   = ___SRAM;
	___DATA_END     = __END_DATA;
	___BSS_START    = __START_BSS;
	___BSS_END      = __END_BSS;
}
